:10
pln Похождения отважного хомяка Семена-10: В снежном плену
btn introduction, Начать игру
btn autors, Автор(ы)
btn instructions, Инструкции
end

:autors
pln Triangle <triangle@zmail.ru> и Apromix <bees@meta.ua>
pln 
pln В главной роли - давно уже покойный хомяк Семен ;)
btn 10, Назад
end

:instructions
pln Тут инструкции совсем не нужны! И так все понятно!
btn 10, Назад
end

:introduction
pln Вы - хомяк по кличке Семен. Если Вы проходили предыдущую игру из серии, то должны помнить, как построили воздушный шар и покинули на нем необитаемый остров, затем как попали в сильную снежную бурю и потерпели авиакатастрофу. На этом пока все.
btn look, Осмотреться
end

:look
pln Немного отоспавшись, Вы выбираетесь из теплой норки. День обещает быть солнечным. Но жутко холодно и ветренно. Вокруг на сколько хватает глаз из Вашего хомячьего роста простирается снежная равнина. Рядом возвышается большое дерево. На его вершине все еще трепыхаются на ветру остатки воздушного шара, на котором Вы сюда прилетели.
pln Вы мотаете головой, стряхивая воспоминания о вчерашней авиакатасторфе как плохой сон. Вы понимаете, что срочно должны поскорее убираться из этой снежной и холодной, огромной и бескрайней пустыни. Вдруг Вы замечаете стадо оленей, пасущихся под одиноким тополем, и у Вас сразу же возникает план побега.
btn house, Спрятаться в норке
end

:house
p В подземном домике тепло и уютно.
if mouse=0 then p #$Но безопасно ли?
if sem1=0 then p #$На земле лежит семечко подсолнуха.
btn out, Выглянуть из норки
if ishouse=0 then btn ihouse, Исследовать подземное жилище
if ishouse=1 then btn lfthouse, Бежать в левое крыло дома
if ishouse=1 then btn rhthouse, Бежать в правое крыло дома
if ishouse=1 then btn ruch, Бежать к подземному ручью
if sem1=0 then btn sjestsem1, Съесть семечко
if Трава then btn splest, Сплести веревку
end

:iout
isout=1
pln Вы исследуете местность вокруг Вашего временного убежища. 
anykey
goto out

:ihouse
ishouse=1
pln Вы исследуете все отнорки и коморки в Вашем временном подземном домике. 
anykey
goto house

:splest
inv- Трава
inv+ Веревка
pln Вы достаете тонкие травинки и начинаете плести длинную крепкую веревку. Вскорее Вы завершаете свою работу.
btn house, Назад
end

:sjestsem1
sem1=1
sila=sila+1
pln Вы съели семечко и почувствовали, как Ваше тело наливается силой.
btn house, Назад
end

:out
pln На улице солнечно, но холодно. Равнина покрыта тонким снежным ковром, из которого то тут, то там торчат пучки сухой травы. Иногда встречаются какие-то колючие кустарники. Рядом с домиком вздымается к небу одинокий огромный тополь. Вдалеке виднеется темная стена леса.
btn house, Нырнуть в норку
if isout=0 then btn iout, Исследовать местность вокруг домика
if isout=1 then btn tree, Бежать к дереву
if isout=1 then btn grass, Подбежать к пучку сухих трав
if isout=1 then btn kust, Подойти к колючему кусту
end

:lfthouse
p Вы очутились в достаточно просторном отнорке. За глинянной стенкой слышно шум текущего ручья. Дальше по корридору находится еще один отнорок. Из него доносится подозрительный шум.
if sem2=0 then p #$На земле лежит семечко подсолнуха.&btn sjestsem2, Съесть семечко
btn mousehouse, Заглянуть в соседний отнорок
if ishouse=1 then btn ruch, Бежать к подземному ручью
btn house, Вернуться в большую нору
end

:rhthouse
p Правый отнорок. Здесь тесно и сыро.
if kop=0 then p #$В глинянной стенке что-то блестит.
if kop=1 then p #$Вы немножко откопали странный камешек, похожий на обычное стекло.
if stek=0 and kop=2 then p #$Вы еще больше откопали камень.
if stek=0 and kop=2 then p #$Вдруг Вы заметили на камне маленькую трещинку.
if sem3=0 then p #$На земле лежит семечко подсолнуха.&btn sjestsem3, Съесть семечко
if kop>0 then btn grkam, Грызть камешек
if sila>=3 then btn kopat, Копать
if kop=2 and stek=0 and Шип then btn ooop, Вставить шип в трещину
if stek=1 and not Стекло then btn stek1, Поднять стеклышко
if stek=1 and not Стекло and Лист then btn stek2, Завернуть стеклышко в листик
btn house, Вернуться в большую нору
end

:sjestsem3
sem3=1
sila=sila+1
pln Вы съели вкусное семечко и почувствовали, как Ваше тело наливается силой.
btn rhthouse, Назад
end

:stek1
pln Вы поднимаете острое стеклышко и засовываете его за щеку... и оно ее с легкостью протыкает... С такой раной Вам не до побега!.. 
end

:grkam
pln Вы грызете камешек, но он не поддается. Вы пытаетесь снова и снова... Пока не ломаете зубы... Беж жубов не до побега!..
end

:stek2
inv- Лист
inv+ Стекло
pln Вы заворачиваете стеклышко в листик и засовываете за щеку.
btn rhthouse, Назад
end

:tree
pln Вы стоите у одинокого тополя. Вокруг полно опавших листьев и веток. В его ветвях завывает холодный ветер.
if not Лист then btn tleaf, Поднять листик
btn ontree, Залезть на дерево
btn out, Бежать к норе
end

:ooop
if sila>=3 then pln Вы вставляете шип в трещину в камне, тужитесь со всей хомячьей силой... и откалываете острый кусочек.&stek=1
btn rhthouse, Назад
end

:kopat
kop=kop+1
if kop<=2 then pln Ловко работая лапками, Вы откапываете камень.&btn rhthouse, Готово
if kop>2 then pln Внезапно камень падает прямо на Вас... Обвал! Вас засыпало в норе...
end

:mousehouse
p Вы заглядываете в отнорок, где устроила свое теплое гнездышко большая полевая мышь.
if mouse=0 then p #$Хозяйка норы злобно сверкает глазками и вот-вот бросится на Вас.
if mouse=1 and zer1=0 then p #$На земле лежит пшеничное зернышко.&btn sjestzer1, Cъесть зернышко
if mouse=0 then btn atk, Атаковать мышь
btn lfthouse, Вернуться назад
end

:tleaf
pln Вы скручиваете в трубочку лист и засовываете его за щеку.
inv+ Лист
btn tree, Назад
end

:sjestzer1
zer1=1
lovk=lovk+1
pln Вы съели маленькое пшеничное зернышко и почувствовали, что стали ловчее.
btn mousehouse, Назад
end

:grass
pln Из снега торчит жмут тонких сухих травинок.
if not Трава and Стекло then btn srezat, Срезать травинки
btn grizttrav, Грызть травинки
btn out, Назад ко входу в дом
end

:srezat
inv+ Трава
pln Ловко орудуя острым камешком, Вы отрезаете несколько травинок и запихиваете их за щеку.
btn grass, Готово
end

:kust
pln Колючий кустарник покрыт снегом и совсем без листьев. На нем полно твердых и острых шипов.
if not Шип then btn tship, Взять шип
btn out, Вернуться назад к норе
end

:atk
if otdih=0 then pln Вы набрасываетесь на противника, но силы слишком не равны и мышь быстро расправляется с Вами...
if not Шип then pln С громким визгом Вы набрасываетесь на мышь, но для дикой полевой мыши Вы не соперник...
if Шип and sila<3 then pln Вы попытались ткнуть мышь "шпагой", но силенок оказалось маловато. Мышь перегрызла Вам горло...
if Шип and sila>=3 and otdih=1 then pln Вы ткнули острым шипом мышь. Мышь громко запищала и убежала. К сожалению Ваше импровизированное оружие сломалось.&inv- Шип&mouse=1&btn mousehouse, Назад
end

:tship
p Вы взбираетесь на куст. Хотя ветки покрыты острыми шипами и по ним тяжело ползти, Вам удается отгрызть один шип.
if not Лист then pln #$Вы быстро заталкиваете шип за щеку, и он протыкает ее насквозь. Вы в страхе скрываетесь в норке, где вскоре и умираете от потери крови...
if Лист then btn zavship, Завернуть шип в листик
end

:sjestsem2
sem2=1
sila=sila+1
pln Вы съели семечко и почувствовали, как Ваше тело наливается силой.
btn lfthouse, Назад
end

:grizttrav
pln Вы долго грызете травинки, но они слишком жесткие и не поддаются. Вы набиваете полный рот травы и погибаете от удушья...
end

:ontree
p Вы с трудом взбираетесь на тополь. Вокруг на сколько хватает глаз простирается бесконечная белоснежная равнина. Здесь высоко и холодно.
if sila<3 then p #$Внезапный порыв ветра подхватывает Вас... Упав с большой высоты, Вы погибаете...&end
if sila>=3 then p #$Сильный пронизывающий ветер пытается Вас сдуть вниз.
btn branch, Залезть на ветку
btn tree, Спуститься с дерева
end

:ruch
pln В этой части норы, где протекает подземный ручеек, сыро и пахнет плесенью. На берегу лежит семечко.
btn estsem, Съесть семечко
if otdih=0 then btn pit, Пить воду
btn house, Вернуться назад
end

:pit
otdih=1
pln Вы осторожно спустились к воде, выпили несколько капель воды из ручья и почувствовали себя отдохнувшим.
btn ruch, Назад
end

:estsem
pln Вы съели заплесневелое семечко и сразу почувствали, что что-то тут не так. Вскоре у Вас заболел живот, а к ночи Вы умерли от отравления...
end

:branch
p Вы оказываетесь на шаткой ветке прямо над пасущимися оленями. Мороз пронизывает Вас до костей. 
if lovk=0 then p #$Внезапный порыв ветра сбрасывает Вас с дерева и Вы разбиваетесь насмерть...&end
if Веревка then p #$Вы решились на побег! Сейчас или никогда!&btn escape, Спрыгнуть на спину оленя
btn ontree, Назад
end

:zavship
inv- Лист
inv+ Шип
pln Вы заворачиваете острый шип в лист и осторожно запихиваете его за щеку.
btn kust, Назад
end

:escape
pln Хоп! Вы спрыгиваете с ветки и приземляетесь оленю точно на спину. Испуганное животное пытается Вас встряхнуть, но Вы ловким движением набрасываете импровизированное "лассо" на шею зверя. Олень со всех ног начинает бежать к лесу, а Вам только этого и надо!
pln
pln Примите мои поздравления! Вы первый хомяк, оседлавший оленя! Вы благополучно миновали все опасности и прошли десятую серию приключений хомяка Семена! Благодаря Вам хомяк остался жив и сможет пройти новые испытания в будущих приключениях! Ждите новые серии!
end
